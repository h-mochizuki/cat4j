import org.gradle.internal.jvm.Jvm

plugins {
    id 'java'
    id 'application'
}

repositories {
    mavenCentral()
}

java {
    // Java21を使用するよう設定
    sourceCompatibility = '21'
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach { options.encoding = 'UTF-8' }

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

application {
    mainClass = 'gmochmoch.cat4j.Cat4j'
}

tasks.named('test') {
    useJUnitPlatform()
}

jar {
    archiveFileName = "${rootProject.name}.jar"
    manifest {
        // ビルド日時を保持するためのMANIFEST-MFを作成する設定
        attributes(
            'Implementation-Title': rootProject.name,
            'Created-By': GradleVersion.current(),
            'Build-Jdk': Jvm.current(),
            'Build-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZ"),
        )
    }
}

distributions {
    main {
        distributionBaseName = rootProject.name
    }
}

startScripts {
    applicationName = rootProject.name
}
